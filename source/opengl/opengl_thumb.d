module opengl.opengl_thumb;

import std.exception : enforce;

import bindbc.opengl;

class DebugTextureBackend {
private:
    GLuint debugThumbTex;
    GLuint debugThumbProg;
    GLint debugThumbMvpLoc = -1;
    GLuint debugThumbVao;
    GLuint debugThumbQuadVbo;
    GLuint debugThumbQuadEbo;

    void ensureThumbVao() {
        if (debugThumbVao == 0) {
            glGenVertexArrays(1, &debugThumbVao);
        }
        if (debugThumbQuadVbo == 0) glGenBuffers(1, &debugThumbQuadVbo);
        if (debugThumbQuadEbo == 0) glGenBuffers(1, &debugThumbQuadEbo);
    }

    void ensureThumbProgram() {
        if (debugThumbProg != 0) return;
        enum string vsSrc = q{
            #version 330 core
            uniform mat4 mvp;
            layout(location = 0) in vec2 inPos;
            layout(location = 1) in vec2 inUv;
            out vec2 vUv;
            void main() {
                gl_Position = mvp * vec4(inPos, 0.0, 1.0);
                vUv = inUv;
            }
        };
        enum string fsSrc = q{
            #version 330 core
            in vec2 vUv;
            layout(location = 0) out vec4 outColor;
            uniform sampler2D albedo;
            void main() {
                outColor = texture(albedo, vUv);
            }
        };
        auto compile = (GLenum kind, string src) {
            GLuint s = glCreateShader(kind);
            const(char)* p = src.ptr;
            glShaderSource(s, 1, &p, null);
            glCompileShader(s);
            GLint ok = 0;
            glGetShaderiv(s, GL_COMPILE_STATUS, &ok);
            enforce(ok == GL_TRUE, "thumb shader compile failed");
            return s;
        };
        GLuint vs = compile(GL_VERTEX_SHADER, vsSrc);
        GLuint fs = compile(GL_FRAGMENT_SHADER, fsSrc);
        debugThumbProg = glCreateProgram();
        glAttachShader(debugThumbProg, vs);
        glAttachShader(debugThumbProg, fs);
        glLinkProgram(debugThumbProg);
        GLint linked = 0;
        glGetProgramiv(debugThumbProg, GL_LINK_STATUS, &linked);
        enforce(linked == GL_TRUE, "thumb shader link failed");
        glDeleteShader(vs);
        glDeleteShader(fs);
        glUseProgram(debugThumbProg);
        GLint albedoLoc = glGetUniformLocation(debugThumbProg, "albedo");
        if (albedoLoc >= 0) glUniform1i(albedoLoc, 0);
        debugThumbMvpLoc = glGetUniformLocation(debugThumbProg, "mvp");
        glUseProgram(0);
    }

public:
    void ensureDebugTestTex() {
        if (debugThumbTex != 0) return;
        const int sz = 48;
        ubyte[sz * sz * 4] pixels;
        foreach (y; 0 .. sz) foreach (x; 0 .. sz) {
            bool on = ((x / 6) ^ (y / 6)) & 1;
            auto idx = (y * sz + x) * 4;
            pixels[idx + 0] = on ? 255 : 30;
            pixels[idx + 1] = on ? 128 : 30;
            pixels[idx + 2] = on ? 64 : 30;
            pixels[idx + 3] = 255;
        }
        glGenTextures(1, &debugThumbTex);
        glBindTexture(GL_TEXTURE_2D, debugThumbTex);
        glTexImage2D(GL_TEXTURE_2D, 0, GL_RGBA8, sz, sz, 0, GL_RGBA, GL_UNSIGNED_BYTE, pixels.ptr);
        glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_LINEAR);
        glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_LINEAR);
        glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_S, GL_CLAMP_TO_EDGE);
        glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_T, GL_CLAMP_TO_EDGE);
    }

    void drawTile(GLuint texId, float x, float y, float size, int screenW, int screenH) {
        if (texId == 0) return;
        ensureThumbProgram();
        ensureThumbVao();
        glBindFramebuffer(GL_FRAMEBUFFER, 0);
        glViewport(0, 0, screenW, screenH);
        glUseProgram(debugThumbProg);
        float left = (x / cast(float)screenW) * 2f - 1f;
        float right = ((x + size) / cast(float)screenW) * 2f - 1f;
        float top = (y / cast(float)screenH) * 2f - 1f;
        float bottom = ((y + size) / cast(float)screenH) * 2f - 1f;
        if (debugThumbMvpLoc >= 0) {
            float[16] ident = [1, 0, 0, 0, 0, 1, 0, 0, 0, 0, 1, 0, 0, 0, 0, 1];
            glUniformMatrix4fv(debugThumbMvpLoc, 1, GL_FALSE, ident.ptr);
        }
        glActiveTexture(GL_TEXTURE0);
        glBindTexture(GL_TEXTURE_2D, texId);
        float[24] verts = [
            left, top, 0, 0,
            right, top, 1, 0,
            left, bottom, 0, 1,
            right, top, 1, 0,
            right, bottom, 1, 1,
            left, bottom, 0, 1
        ];
        glBindVertexArray(debugThumbVao);
        glBindBuffer(GL_ARRAY_BUFFER, debugThumbQuadVbo);
        glBufferData(GL_ARRAY_BUFFER, verts.length * float.sizeof, verts.ptr, GL_STREAM_DRAW);
        glEnableVertexAttribArray(0);
        glVertexAttribPointer(0, 2, GL_FLOAT, GL_FALSE, cast(int)(4 * float.sizeof), cast(void*)0);
        glEnableVertexAttribArray(1);
        glVertexAttribPointer(1, 2, GL_FLOAT, GL_FALSE, cast(int)(4 * float.sizeof), cast(void*)(2 * float.sizeof));
        glDisableVertexAttribArray(2);
        glDisableVertexAttribArray(3);
        glDisableVertexAttribArray(4);
        glDisableVertexAttribArray(5);
        glDrawArrays(GL_TRIANGLES, 0, 6);
        glBindVertexArray(0);
    }

    void renderThumbnailGrid(int screenW, int screenH, scope const(GLuint)[] textureIds) {
        GLint prevFbo = 0, prevProgram = 0, prevVao = 0, prevDrawBuf = 0;
        GLboolean prevDepth = 0, prevStencil = 0, prevCull = 0, prevScissor = 0;
        GLint[4] prevViewport;
        glGetIntegerv(GL_DRAW_FRAMEBUFFER_BINDING, &prevFbo);
        glGetIntegerv(GL_CURRENT_PROGRAM, &prevProgram);
        glGetIntegerv(GL_VERTEX_ARRAY_BINDING, &prevVao);
        glGetIntegerv(GL_DRAW_BUFFER, &prevDrawBuf);
        glGetIntegerv(GL_VIEWPORT, prevViewport.ptr);
        prevDepth = glIsEnabled(GL_DEPTH_TEST);
        prevStencil = glIsEnabled(GL_STENCIL_TEST);
        prevCull = glIsEnabled(GL_CULL_FACE);
        prevScissor = glIsEnabled(GL_SCISSOR_TEST);

        glBindFramebuffer(GL_FRAMEBUFFER, 0);
        glDisable(GL_DEPTH_TEST);
        glDisable(GL_STENCIL_TEST);
        glDisable(GL_CULL_FACE);
        glViewport(0, 0, screenW, screenH);
        glDrawBuffer(GL_BACK);

        glEnable(GL_SCISSOR_TEST);
        const float tile = 48;
        const float pad = 2;
        float sidebarWidthPx = (tile + pad) * 8;
        glScissor(0, 0, cast(int)sidebarWidthPx, screenH);
        glClearColor(0.18f, 0.18f, 0.18f, 1.0f);
        glClear(GL_COLOR_BUFFER_BIT);
        glDisable(GL_SCISSOR_TEST);

        ensureDebugTestTex();
        float tx = pad;
        float ty = pad;
        drawTile(debugTestTextureId(), tx, ty, tile, screenW, screenH);
        ty += tile + pad;
        foreach (texId; textureIds) {
            drawTile(texId, tx, ty, tile, screenW, screenH);
            ty += tile + pad;
            if (ty + tile > screenH - pad) {
                ty = pad;
                tx += tile + pad;
            }
        }
        glGetError();

        if (prevDepth) glEnable(GL_DEPTH_TEST); else glDisable(GL_DEPTH_TEST);
        if (prevStencil) glEnable(GL_STENCIL_TEST); else glDisable(GL_STENCIL_TEST);
        if (prevCull) glEnable(GL_CULL_FACE); else glDisable(GL_CULL_FACE);
        if (prevScissor) glEnable(GL_SCISSOR_TEST); else glDisable(GL_SCISSOR_TEST);
        glBindFramebuffer(GL_FRAMEBUFFER, prevFbo);
        glViewport(prevViewport[0], prevViewport[1], prevViewport[2], prevViewport[3]);
        glDrawBuffer(prevDrawBuf);
        glUseProgram(prevProgram);
        glBindVertexArray(prevVao);
    }

    GLuint debugTestTextureId() const {
        return debugThumbTex;
    }
}

private __gshared DebugTextureBackend cachedDebugTextureBackend;

DebugTextureBackend currentDebugTextureBackend() {
    if (cachedDebugTextureBackend is null) {
        cachedDebugTextureBackend = new DebugTextureBackend();
    }
    return cachedDebugTextureBackend;
}
